: TAB-WIDTH 8 ;

Var integer cursor-x
Var integer cursor-y
Var integer cursor-x-sticky
Var object lines-buffer
Var object status
Var object command

@dup Sys Fs read
'\n' String split
set:lines-buffer

"editing " @swap @concat set:status

: f
	#dup 0 #swap Sys Terminal set-cursor
	@dup @refs Sys Terminal print
	;

: #drop2 #swap #drop ;
: #dup2 #2dup #drop ;

: lines-count lines-buffer @refcount ;
: char-width if '\t' = then TAB-WIDTH else 1 end ;
Var integer x
: line-bytecount lines-buffer @refs @bytecount ;
: string-visual-width
	0 set:x
	0 if #dup @dup @bytecount < then
		#dup @dup @byte char-width x + set:x
		1 +
	repeat #drop @drop x ;
: line-visual-width lines-buffer @refs string-visual-width ;

: string-slice 0 0 @slice ;

: current-line cursor-y lines-buffer @refs ;
: current-line-bytecount cursor-y line-bytecount ;
: current-line-slice current-line string-slice ;
: current-line-slice-start 0 #swap current-line-slice ;
: current-line-slice-end current-line @bytecount current-line-slice ;
: current-line-cut #swap current-line-slice-start current-line-slice-end ;

: window-height Sys Terminal size #drop2 ;

: render-text
	0 0 Sys Terminal set-cursor
	lines-buffer
	window-height
	@dup @refcount #min
	1 -

	0 if #2dup > then
		#dup f
		1 +
	repeat @drop #drop
	;

: cursor cursor-x cursor-y ;
: set:cursor set:cursor-y set:cursor-x ;

: move-cursor-x cursor-x + set:cursor-x cursor-x set:cursor-x-sticky ;
: move-cursor-y cursor-y + set:cursor-y ;
: cursor-up    -1 move-cursor-y ;
: cursor-down   1 move-cursor-y ;
: cursor-left  -1 move-cursor-x ;
: cursor-right  1 move-cursor-x ;

: cursor-clamp
	cursor-y 0 #max lines-count 1 - #min set:cursor-y
	cursor-x-sticky 0 #max current-line-bytecount #min set:cursor-x ;

: set-current-line
	0 0  0 cursor-y                lines-buffer @slice
	@swap @intoref @concat
	0 0  cursor-y 1 + lines-count  lines-buffer @slice
	@concat set:lines-buffer ;

: delete-current-line
	0 0  0 cursor-y                lines-buffer @slice
	0 0  cursor-y 1 + lines-count  lines-buffer @slice
	@concat set:lines-buffer
	cursor-up ;

: delete-newline-start
	if cursor-y 0 > then
		current-line delete-current-line
		current-line @swap @concat set-current-line
	end ;
: delete-char
	if cursor-x 0 > then
		cursor-x 1 - cursor-x current-line-cut
		@concat set-current-line
		cursor-left
	else
		delete-newline-start
	end ;

: insert-char
	cursor-x cursor-x current-line-cut
	@intobyte @swap
	@concat @concat set-current-line
	cursor-right ;


: keycode 1 21 #bit:shl 1 - #bit:and ;

: is-press #dup 27 #bit:shr 0b01 = ;

: KEY_UP    0b11 19 #bit:shl 0b00 #bit:or ;
: KEY_DOWN  0b11 19 #bit:shl 0b01 #bit:or ;
: KEY_LEFT  0b11 19 #bit:shl 0b10 #bit:or ;
: KEY_RIGHT 0b11 19 #bit:shl 0b11 #bit:or ;
: KEY_DELETE    0b11 19 #bit:shl 0x10 #bit:or ;
: KEY_BACKSPACE 0b11 19 #bit:shl 0x11 #bit:or ;

: status:unmapped-keycode "unmapped keycode " String decimal @concat set:status ;

: is-special #drop 0 ;

: render-status
	0 window-height 2 - Sys Terminal set-cursor
	status Sys Terminal print ;

: render-cursor
	current-line  0 cursor-x 0 0 @slice  string-visual-width
	cursor-y Sys Terminal set-cursor ;

: render-command
	0 window-height 1 - Sys Terminal set-cursor
	Sys Terminal clear-line
	":" command @concat Sys Terminal print ;

: getchar
	0 if 1 <> then
		Sys Terminal wait
		is-press
	repeat keycode ;

: input-command-one render-command Sys Terminal flush getchar ;
: input-command-insert-char command @intobyte @concat set:command ;
: len command @bytecount ;
: backspace if len 0 > then command 0 len 1 - string-slice set:command end ;
: input-command-parsechar
	if #dup KEY_BACKSPACE =
	then #drop backspace
	else command @intobyte @concat set:command
	end ;
: input-command
	"" set:command
	input-command-one if #dup '\n' <> then
		input-command-parsechar
	input-command-one repeat #drop ;

: decode #dup if #dup is-special then status:unmapped-keycode else insert-char end ;
: decode if #dup ':' = then input-command else decode end ;
: decode if #dup KEY_UP    = then cursor-up    else decode end ;
: decode if #dup KEY_DOWN  = then cursor-down  else decode end ;
: decode if #dup KEY_LEFT  = then cursor-left  else decode end ;
: decode if #dup KEY_RIGHT = then cursor-right else decode end ;
: decode if #dup KEY_BACKSPACE = then delete-char else decode end ;
: decode if is-press then keycode decode end #drop ;

if 1 then
	Sys Terminal clear
	cursor-clamp
	render-text
	render-status
	render-command
	render-cursor
	Sys Terminal flush
	Sys Terminal wait decode
repeat
